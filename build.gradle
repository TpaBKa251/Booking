plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.owasp.dependencycheck' version '12.1.1'
    id "org.sonarqube" version "7.0.1.6134"
    id 'jacoco'
}

group = 'ru.tpu.hostel'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

def repoUser = project.findProperty("privateRepoUsername") ?: System.getenv("INTERNAL_REPO_LOGIN")
def repoPass = project.findProperty("privateRepoPassword") ?: System.getenv("INTERNAL_REPO_PASSWORD")

repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
    maven {
        url = "http://82.202.138.26:8081/repository/internal/"
        allowInsecureProtocol = true
        credentials {
            username = repoUser
            password = repoPass
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.0.0"
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "TpaBKa251_Booking"
        property "sonar.organization", "hostel-service"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.token", System.getenv("SONAR_TOKEN")
        property "sonar.coverage.jacoco.xmlReportPaths", layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml")
        property "sonar.coverage.exclusions", "**/*Generated*.java,**/model/*.java"
        property "sonar.java.binaries", "build/classes/java/main"
    }
}

ext {
    set('internalLibraryVersion', "1.4.1")
}

dependencies {
    // Внутрення библиотека
    implementation "ru.tpu.hostel:hostel-core:${internalLibraryVersion}"
    implementation "ru.tpu.hostel:hostel-feign:${internalLibraryVersion}"
    implementation "ru.tpu.hostel:hostel-amqp:${internalLibraryVersion}"
    implementation "ru.tpu.hostel:notification:${internalLibraryVersion}"

    // Spring Boot starters
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.redisson:redisson-spring-boot-starter:3.49.0'

    // Spring Cloud
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.3.0'

    // БД
    runtimeOnly 'org.postgresql:postgresql:42.7.8'
    implementation 'org.flywaydb:flyway-core:11.17.2'
    implementation 'org.flywaydb:flyway-database-postgresql:11.17.2'

    // Доки
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'

    // Утилиты
    implementation 'org.mapstruct:mapstruct:1.6.3'
    implementation 'commons-io:commons-io:2.19.0'
    implementation 'org.apache.commons:commons-compress:1.27.1'
    implementation 'org.apache.commons:commons-collections4:4.5.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.19.0'
    implementation 'com.squareup:javapoet:1.13.0'

    // Безопасность
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
    implementation 'org.springframework.security:spring-security-crypto:6.5.0'

    // Наблюдение (трассировка, метрики, экспорт)
    implementation platform("io.opentelemetry:opentelemetry-bom:1.49.0")
    implementation platform("io.opentelemetry:opentelemetry-bom-alpha:1.49.0-alpha")
    implementation platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom:2.22.0")
    implementation platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom-alpha:2.22.0-alpha")

    implementation 'io.micrometer:micrometer-tracing-bridge-otel:1.6.0'
    implementation 'io.opentelemetry:opentelemetry-exporter-otlp'
    implementation 'io.opentelemetry.instrumentation:opentelemetry-jdbc'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.16.0'
    implementation 'com.github.loki4j:loki-logback-appender:2.0.1'
    implementation 'io.opentelemetry.semconv:opentelemetry-semconv'
    implementation 'io.opentelemetry.instrumentation:opentelemetry-hibernate-common:1.13.1-alpha'
    implementation 'net.logstash.logback:logstash-logback-encoder:9.0'

    // Cache
    implementation 'com.github.ben-manes.caffeine:caffeine:3.2.3'
    implementation 'de.javakaffee:kryo-serializers:0.45'

    // Jakarta
    implementation 'jakarta.validation:jakarta.validation-api:3.1.1'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    // MapStruct
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    // Тесты
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.8'
    testImplementation 'org.testcontainers:postgresql:1.19.8'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    testImplementation 'org.assertj:assertj-core:3.26.0'
    testImplementation 'org.instancio:instancio-core:4.6.0'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
        html.required.set(false)
        csv.required.set(false)
    }
}

jacoco {
    toolVersion = "0.8.14"
}

jar {
    manifest {
        attributes 'Main-Class': 'ru.tpu.hostel.booking.BookingApplication'
    }
}